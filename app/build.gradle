plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.ksp
    alias libs.plugins.hilt
    alias libs.plugins.gms
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "nz.mikhailov.motiv"
        minSdk 26
        targetSdk 34
        versionCode findProperty("android.injected.version.code") as Integer ?: 1
        versionName findProperty("android.injected.version.name") ?: "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidxComposeCompiler.get().toString()
    }
    packagingOptions {
        resources {
            exclude "META-INF/AL2.0"
            exclude "META-INF/LGPL2.1"
        }
    }
    namespace "nz.mikhailov.motiv"
}

dependencies {

    implementation project(':core:common')
    implementation project(':core:design')
    implementation project(':data:rewards')
    implementation project(':data:transactions')
    implementation project(':data:weight')
    implementation project(':feature:settings')
    implementation platform(libs.androidx.compose.bom)
    implementation platform(libs.firebase.bom)
    implementation libs.androidx.compose.material // some components are missing in material3
    implementation libs.androidx.compose.material.iconsExtended
    implementation libs.androidx.compose.material3
    implementation libs.androidx.compose.runtime.livedata
    implementation libs.androidx.compose.ui.tooling
    implementation libs.androidx.navigation.compose
    implementation libs.androidx.hilt.navigation.compose
    implementation libs.firebase.analytics
    implementation libs.firebase.function
    implementation libs.firebase.remote.config

    debugImplementation libs.firebase.appcheck.debug
    releaseImplementation libs.firebase.appcheck.integrity

    implementation libs.hilt.android
    ksp libs.hilt.compiler

    implementation libs.androidx.lifecycle.livedata.ktx

    debugImplementation libs.androidx.compose.ui.testManifest

    testImplementation libs.assertj.core
    testImplementation libs.junit4

    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.compose.ui.test
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.androidx.test.ext
    androidTestImplementation libs.assertj.core
}